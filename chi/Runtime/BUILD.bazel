load("//:tools/embed.bzl", "embed_file_in_cpp")

runtime_srcs = [
    "arc.c",
    "main.c",
]

[genrule(
    name = "runtime_%s" % f,
    srcs = [
        f,
        "@clang",
    ],
    outs = ["%s.bc" % f],
    cmd = "$(location @clang//:clang) -c -emit-llvm $(location %s) -o $(location %s.bc)" % (
        f,
        f,
    ),
    message = "Generating %s.bc for chigraph runtime..." % f,
) for f in runtime_srcs]

genrule(
    name = "runtime_linkbc",
    srcs = [
        "@llvm//:llvm-link",
    ] + ["%s.bc" % f for f in runtime_srcs],
    outs = ["runtime.bc"],
    cmd = "$(location @llvm//:llvm-link) " + " ".join(["$(location %s.bc)" % f for f in runtime_srcs]) + " -o $(location runtime.bc)",
    message = "Linking chigraph support library...",
)

embed_file_in_cpp(
    name = "runtime_embedded",
    file = "runtime.bc",
    function_name = "embedded_chigraph_runtime_bitcode",
)

cc_library(
    name = "Runtime",
    srcs = [
        "Runtime.cpp",
        "runtime_embedded.cpp",
    ],
    hdrs = ["Runtime.hpp"],
    visibility = [
        "//chi/Core:__pkg__",
    ],
    deps = [
        "//chi/CodeSupport",
    ],
)
